apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'codenarc'

def utilsPath = 'utils'
def pluginsPath = 'plugins'
def jenkinsVersion = '1.651'

repositories {
    mavenCentral()
    jcenter()
    maven { url 'http://repo.jenkins-ci.org/releases/' }
}

configurations {
    libs
}

dependencies {
    libs 'org.apache.ivy:ivy:2.4.0@jar'
    compile 'org.codehaus.groovy:groovy-all:2.1.3'
    compile 'javax.servlet:servlet-api:2.4'
    compile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}"
    compile 'org.kohsuke:github-api:1.70'
    compile 'org.jenkins-ci.plugins:ghprb:1.22.4@jar'
    compile 'org.jenkins-ci.plugins:ec2:1.28@jar'
    compile 'org.jenkins-ci.plugins:matrix-auth:1.2@jar'
    compile 'org.jenkins-ci.plugins:github-oauth:0.24@jar'
    compile 'org.jenkins-ci.plugins:credentials:1.24@jar'
    compile 'org.jenkins-ci.plugins:plain-credentials:1.1@jar'
    compile 'org.jenkins-ci.plugins:ssh-credentials:1.11@jar'
    compile 'org.jenkins-ci.plugins:gradle:1.24@jar'
    compile 'org.jenkins-ci.plugins:jobConfigHistory:2.10@jar'
    compile 'org.jvnet.hudson.plugins:hipchat:0.1.9@jar'
    compile 'org.jenkins-ci.plugins:mailer:1.16@jar'
    compile 'com.amazonaws:aws-java-sdk:1.11.178'
    testCompile 'org.codehaus.groovy:groovy-all:2.1.3'
    testCompile 'javax.servlet:servlet-api:2.4'
    testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}"
}

codenarc {
    toolVersion = '0.25.2'
    configFile = file('codenarc/rules.groovy')
    ignoreFailures = false
    maxPriority1Violations = 0
    maxPriority2Violations = 0
    maxPriority3Violations = 0

    // Display codenarc violations in the console for travis builds
    // otherwise, default to creating an html report
    if (System.getenv('CI_SYSTEM') == 'travis') {
        reportFormat = 'console'
    }
}

task libs(type: Copy) {
    into "${utilsPath}"
    from configurations.libs
}

clean {
    delete "${utilsPath}"
    delete "${pluginsPath}"
}

tasks.withType(GroovyCompile) {
    groovyClasspath += configurations.libs
}
